fmt := import("fmt")
ll := import(":ll")
render := import(":render")
validation := import(":validation")
assets := import(":assets")
smart := import(":smart")

waiterTpl := assets.importTemplate(":waiter.waiter")

/**
 * Create new waiter, that will wait for all resources given to wait() and pass <pass> to <output> afterwards.
 *
 * @param pass: smart.field | smart.resource - reference to something that should be passed to the output
 *                                             once all waits get ready.
 */
builder := func(pass) {
	self := undefined

	/** type: smart.reference[] */
	whatToWait := []

	self = ll.toStrict({
		/**
		 * Wait for given reference to become ready.
		 *
		 * @param ref: smart.field | smart.resource - reference to resource to wait for ResourceReady
		 *                                            state before passing <pass> to the output of waiter
		 */
		wait: func(ref) {
			validation.assertJsonSchema(ref, validation.reference, "waiter.builder.wait(ref): ref should be a reference to resource or field")
			whatToWait = append(whatToWait, ref)

			return self
		},

		/**
		 * Build the waiter.
		 *
		 * @returns {object} The waiter instance, that has `output()` method to get the reference to result.
		 */
		build: func() {
			ll.assert(len(whatToWait) > 0, "waiter.builder.build(): at least one wait is required")

			trigger := undefined

			if len(whatToWait) == 1 {
				trigger = whatToWait[0]
			} else {
				triggerBuilder := smart.ephemeralMapBuilder()
				for i, v in whatToWait {
					triggerBuilder.addRef(fmt.sprintf("input_%d", i), v)
				}
				trigger = triggerBuilder.build()
			}

			tpl := render.createEphemeral(waiterTpl, {
				pass: pass,
				trigger: trigger
			})

			return ll.toStrict({
				output: func() {
					return tpl.output("output")
				}
			})
		}
	})

	return self
}

export ll.toStrict({
	builder: builder
})
