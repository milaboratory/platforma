/* Low level pl API */

plapi := import("plapi")
fmt := import("fmt")
log := import("log")
text := import("text")

/**
 * Prints message to the pl log
 */
print := func(txt, ...args) {
	plapi.print(txt, args...)
}

/**
 * Panics in a template, prints the error and
 * stops the execution of the template.
 * After that, the controller will set errors to outputs.
 */
panic := func(msg, ...args) {
	txt := fmt.sprintf(msg, args...)
	plapi.print(txt)

	// next statement actually terminates the execution
	plapi.setTemplateError(txt)
}

/**
 * Asserts a given condition and panics if it is not met
 */
assert := func(condition, msg, ...args) {
	if !condition {
		panic("assertion error: condition failed: "+msg, args...)
	}
}

/**
 * Checks whether the given object is a strict map
 *
 * @param obj any
 * @return strict: bool - whether obj is a 'strict' map
 */
isStrict := func(obj) {
	return plapi.isStrictMap(obj)
}

/**
 * Checks whether the given object is a map, a strict map or an immutable map.
 *
 * @param obj any
 * @return bool whether obj is any kind of map object (map/immutable map/strict map)
 */
isMap := func(obj) {
	return is_map(obj) || is_immutable_map(obj) || isStrict(obj)
}

/**
 * Converts 'strict' map into a normal one
 *
 * @param r: map|strictMap (will panic if not)
 *
 * @return map: std map
 */
fromStrict := func(r) {
	if is_map(r) {
		return r
	}

	if !isStrict(r) {
		panic("not a map", r)
	}

	return plapi.strictToMap(r)
}

/**
 * If input is a strict map, converts it to a normal one, otherwise returns the object as is
 *
 * @param r: any object (possible strict map)
 * @return map: tengo object
 */
ensureNonStrict := func(r) {
	if isStrict(r) {
		return fromStrict(r)
	}

	return r
}

/**
 * Converts map into a 'strict' map, which will panic on
 * access to non-existing elements.
 *
 * @param m: map|strictMap
 * @param recursive: boolean - convert also all child maps
 *
 * @return strict: strictMap
 */
toStrict := func(v, ...recursive) {
	if len(recursive) > 0 && recursive[0] {
		if isMap(v) {
			for k, vv in v {
				v[k] = toStrict(vv)
			}
		} else if is_array(v) {
			for i, vv in v {
				if isMap(vv) {
					v[i] = toStrict(vv)
				}
			}
		}

		if !isMap(v) {
			return v // for recursive transformation, accept the top-level value to be of any type (i.e. array of maps)
		}
	}

	if isStrict(v) {
		return v
	}

	if(!isMap(v)) {
		panic("toStrict(<v>): <v> is not a map", v)
	}
	return plapi.mapToStrict(v)
}

/**
 * Safe way to check if method exists in a builtin module or a library
 *
 * @param module: module - builtin module or tengo library
 * @param methodName: string - name of a method to be checked
 *
 * @return bool - true when method exists, false otherwise
 */
methodExists := func(module, methodName) {
	if isStrict(module) {
		if !is_callable(plapi.mapHasKey) {
			module = fromStrict(module)

		} else {
			if !plapi.mapHasKey(module, methodName) {
				return false
			}
		}
	}

	method := module[methodName]

	if is_undefined(method) {
		return false
	}

	return true
}

/**
 * Provides current API version available at runtime
 */
apiVersion := func() {
	if !methodExists(plapi, "apiVersion") {
		return 0 // before we introduced apiVersion
	}

	return plapi.apiVersion
}

/**
 * Creates a field Id struct from given resource id and field name
 */
fieldId := func(resourceId, fieldName) {
	return plapi.newFieldID(resourceId, fieldName)
}

/**
 * Checks if <val> can be used as field ID
 */
isFieldId := func(val) {
	if !isStrict(val) {
		return false
	}
	v := fromStrict(val)
	return v["ResourceID"] != undefined && v["Name"] != undefined
}

/**
 * Converts reference ID (resource ID or field ID) to stable string representation, suitable for use as map keys
 */
idToString := func(referenceID) {
	if isFieldId(referenceID) {
		return fmt.sprintf("0x%X/%s", referenceID.ResourceID, referenceID.Name)
	}
	if is_int(referenceID) {
		return fmt.sprintf("0x%X", referenceID)
	}
	panic("ll.idToString: <referenceID> cannot be used as resource or field ID")
}

/**
 * Returns current template renderer
 */
getCurrentTemplateRenderer := func() {
	return plapi.getTemplate()
}

/**
 * @return isInit: bool - true when current template is initializing (executed for the first time)
 */
isInitializing := func() {
	return plapi.isInit
}

/**
 * Get the list of executors available in current Platforma installation.
 *
 * @return executors: []string - list of executors, say ['executor', 'docker', ...]
 */
getExecutorsList := func() {
	return plapi.getExecutors()
}

/**
 * Joins path elements into a single path.
 *
 * @param elems: ...string - any number of elements.
 */
filepathJoin := func(...elems) {
	return plapi.filepathJoin(elems...)
}

/**
 * Like filepathJoin, joins elements to a single path,
 * but can be used safely in regexps.
 *
 * @param elems: ...string - any number of elements.
 */
filepathRegexpJoin := func(...elems) {
	escapedSeparator := plapi.regexpQuoteMeta(plapi.osSeparator)
	return text.join(elems, escapedSeparator)
}

/**
 * Joins elements of the filepath and canonizes it, i.e.
 * semantically resolves ../ and ./ and changes slash
 * to a pathSeparator.
 *
 * @param elems: ...string - any number of elements.
 */
filepathCanonize := func(...elem) {
	return plapi.filepathCanonize(elem...)
}

/**
 * Returns a path separator. "/" on Unix-like systems, "\\" on Windows.
 *
 * @return separator: string
 */
pathSeparator := plapi.osSeparator

/**
 * Parses URL and returns a structure containing separe parts of the URL.
 *
 * The general form represented is:
 *
 *   [scheme:][//[userinfo@]host][/]path[?query][#fragment]
 *
 * URLs that do not start with a slash after the scheme are interpreted as:
 *
 *   scheme:opaque[?query][#fragment]
 *
 * Returns the floowing structure:
 *
 *   {
 *     Scheme: string,
 *     Host: string,
 *     Port: number,
 *     Path: string,
 *     RawPath: string,
 *     Query: string
 *   }
 *
 * @param url: string with the url
 * @return url structure: (see above)
 */
parseUrl := func(url) {
	return plapi.urlParse(url)
}

/**
 * Returns a persistent tengo map, which cab be used is a simple kv-store associated with the renderer instance.
 * It is persisted between template runs, but the cons are we could store only serializable data here (tengo maps, strings, slices, numbers etc).
 * DEPRECATED: use `ll.getPersistentCtx`
 */
getCtx := func() {
	if methodExists(plapi, "getPersistentCtx") {
		return plapi.getPersistentCtx()
	}

	// DEPRECATED
	return plapi.getCtx()
}

/**
 * Returns a persistent tengo map context, which can be used is a simple kv-store associated with the renderer instance.
 * It is persisted between template runs, but the cons are we could store only serializable data here (tengo maps, strings, slices, numbers etc).
 */
getPersistentCtx := func() {
	if methodExists(plapi, "getPersistentCtx") {
		return plapi.getPersistentCtx()
	}

	// DEPRECATED
	return plapi.getCtx()
}

/**
 * Returns
 * It's a map for cases when
 * we need to have a place for global variables.
 * For example, tengo modules instantiate on each import and don't store global state.
 * So we can use this map to store temporary data.
 *
 * Pros: it can store non-serializable data, like methods.
 * Cons: it's not saved after the script execution.
 */
getExecutionCtx := func() {
	assert(methodExists(plapi, "getExecutionCtx"), "ll.getExecutionCtx: method does not exists in this backend instance.")
	return plapi.getExecutionCtx()
}

/**
 * Returns a storage for global variables of the module.
 * We have a problem that every import instantiates its own instance of a module,
 * and they don't share state between each other.
 * the execution context could help to store a state between these modules.
 */
moduleVars := func(moduleName) {
	assert(is_string(moduleName), "ll.moduleVars: moduleName must be a string")

	name := "module/" + moduleName

	ctx := getExecutionCtx()
	if is_undefined(ctx[name]) {
		ctx[name] = {}
	}

	return {
		get: func(k) {
			return ctx[name][k]
		},

		set: func(k, v) {
			ctx[name][k] = v
		}
	}
}

/**
 * Returns sha256 hash from bytes.
 */
sha256Encode := func(b) {
	assert(methodExists(plapi, "sha256Encode"), "ll.sha256Encode: method does not exists in this backend instance.")
	return plapi.sha256Encode(b)
}

/**
 * Returns gzip encoded bytes from bytes.
 */
gzipEncode := func(b) {
	assert(methodExists(plapi, "gzipEncode"), "ll.gzipEncode: method does not exists in this backend instance.")
	return plapi.gzipEncode(b)
}

/**
 * Decodes gzip bytes from bytes.
 */
gzipDecode := func(b) {
	assert(methodExists(plapi, "gzipDecode"), "ll.gzipDecode: method does not exists in this backend instance.")
	return plapi.gzipDecode(b)
}

/**
 * Encodes bytes to base32 string.
 */
base32Encode := func(b) {
	assert(methodExists(plapi, "base32Encode"), "ll.base32Encode: method does not exists in this backend instance.")
	return plapi.base32Encode(b)
}

/**
 * Encodes bytes to base32hex string.
 *
 *	One property with this alphabet, which the base64 and base32
 *	alphabets lack, is that encoded data maintains its sort order when
 *	the encoded data is compared bit-wise.
 */
base32HexEncode := func(b) {
	assert(methodExists(plapi, "base32HexEncode"), "ll.base32HexEncode: method does not exists in this backend instance.")
	return plapi.base32HexEncode(b)
}

/**
 * Decodes base32 string to bytes
 */
base32Decode := func(s) {
	assert(methodExists(plapi, "base32Decode"), "ll.base32Decode: method does not exists in this backend instance.")
	return plapi.base32Decode(s)
}

/**
 * Decodes base32hex string to bytes.
 *
 *	One property with this encoding, which the base64 and base32
 *	alphabets lack, is that encoded data maintains its sort order when
 *	the encoded data is compared bit-wise.
 */
base32HexDecode := func(s) {
	assert(methodExists(plapi, "base32HexDecode"), "ll.base32HexDecode: method does not exists in this backend instance.")
	return plapi.base32HexDecode(s)
}

/**
 * Returns the size of the blob in bytes.
 */
getBlobSize := func(resourceID) {
	assert(methodExists(plapi, "getBlobSize"), "ll.getBlobSize: method does not exists in this backend instance.")
	return plapi.getBlobSize(resourceID)
}

export toStrict({
	apiVersion: apiVersion,
	print: print,
	panic: panic,
	assert: assert,
	methodExists: methodExists,
	isStrict: isStrict,
	isMap: isMap,
	fromStrict: fromStrict,
	ensureNonStrict: ensureNonStrict,
	toStrict: toStrict,
	fieldId: fieldId,
	isFieldId: isFieldId,
	idToString: idToString,
	getCurrentTemplateRenderer: getCurrentTemplateRenderer,
	isInitializing: isInitializing,
	getExecutorsList: getExecutorsList,
	filepathJoin: filepathJoin,
	filepathRegexpJoin: filepathRegexpJoin,
	pathSeparator: pathSeparator,
	filepathCanonize: filepathCanonize,
	parseUrl: parseUrl,
	getCtx: getCtx,
	getPersistentCtx: getPersistentCtx,
	getExecutionCtx: getExecutionCtx,
	moduleVars: moduleVars,
	sha256Encode: sha256Encode,
	gzipEncode: gzipEncode,
	gzipDecode: gzipDecode,
	base32Encode: base32Encode,
	base32HexEncode: base32HexEncode,
	base32Decode: base32Decode,
	base32HexDecode: base32HexDecode,
	getBlobSize: getBlobSize
})
