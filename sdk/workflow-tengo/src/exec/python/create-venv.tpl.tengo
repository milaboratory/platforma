/**
 * Creates new python virtual environment.
 */

self := import(":tpl.light")

feats := import(":feats")
render := import(":render")
workdir := import(":workdir")
runcmd := import(":exec.runcmd")
assets := import(":assets")
path := import(":path")
validation := import(":validation")
runenv := import(":exec.runenv")
internal := import(":exec.python.internal")
constants := import(":exec.constants")
pkg := import(":exec.package")

_createVenv := func(pythonPackage, pythonDescriptor) {
	wdBefore := workdir.builder().
		inQueue(pkg.DEFAULT_ALLOCATION_QUEUE).
		cpu(pkg.DEFAULT_ALLOCATION_CPU).
		mem(pkg.DEFAULT_ALLOCATION_RAM).
		build()

	// DEPRECATED: remove after 15 Mar 2025
	t := feats.commandExpressions ? constants.ARG_TYPE_EXPRESSION : constants.ARG_TYPE_VAR
	pythonRule := feats.commandExpressions ? "python" : "{python}"

	venvBuilder := runcmd.builder(wdBefore)
	venvBuilder.
		ref("python-run-environment", pythonPackage).
		inQueue(pkg.DEFAULT_ALLOCATION_QUEUE).
		cpu(pkg.DEFAULT_ALLOCATION_CPU).
		mem(pkg.DEFAULT_ALLOCATION_RAM).
		substitutionRule(pythonRule, "python-run-environment").
		addPathTyped(t, path.join("{python}", pythonDescriptor.binDir))

	venvBuilder.
		cmd("python").
		arg("-m").
		arg("venv").
		arg("./venv")

	venvBuilder.
		stdout("./output").
		stderr("./output").
		nErrorLines(20)

	venv := venvBuilder.build()

	wdWithVenv := venv.workdir

	runEnvSaver := runenv.saveBuilder(wdWithVenv).
		envType("python").
		addPath("venv").
		addPackage("python", pythonPackage)

	return runEnvSaver.build().runEnv()
}


/**
 * Detects what OS is used on runner side and returns 'Scripts' for windows or 'bin' for unix.
 * This is because python under windows forcefully puts all its binaries into 'Scripts' dir when creating venv
 * and we can't control it.
 */
_pythonVenvBinDir := func(pythonPackage, pythonDescriptor) {
	result := render.createEphemeral(
		assets.importTemplate(":exec.python.venv-bin-path"),
		{
			pythonPackage: pythonPackage,
			pythonDescriptor: pythonDescriptor
		}
	)

	return {
       binDir: result.output("binPath"),
       osPathDelimeter: result.output("osPathDelimeter")
    }
}

installPyDeps := assets.importTemplate(":exec.python.install-deps")

self.defineOutputs("binDir", "runEnv", "osPathDelimeter")

self.body(func(inputs) {
	// assert we have inputs we expect.
	validation.assertType(inputs, internal.pythonVenvInputSchema)

	pythonPackage := inputs.python
	pythonDescriptor := inputs.pythonDescriptor

	venv := _createVenv(pythonPackage, pythonDescriptor)

    venvParams := _pythonVenvBinDir(pythonPackage, pythonDescriptor)

	installDeps := render.createEphemeral(installPyDeps, {
		python: pythonPackage,
		venv: venv,
		toolset: inputs.toolset,
		dependencies: inputs.dependencies,
		pythonBinDir: pythonDescriptor.binDir,
		pythonVenvBinDir: venvParams.binDir
	})

	return {
		binDir: venvParams.binDir,
        osPathDelimeter: venvParams.osPathDelimeter,
		runEnv: installDeps.output("runEnv")
	}
})
