/** The library provides all things to work with a monetization. */

ll := import(":ll")
feats := import(":feats")

validation := import(":validation")
assets := import(":assets")
internal := import(":exec.internal")
fmt := import("fmt")
text := import("text")
runcmd := import(":exec.runcmd")
workdir := import(":workdir")
render := import(":render")
constants := import(":exec.constants")

_JWT_TOKEN_RESULT_ENV := "PLATFORMA_MNZ_JWT"

/** Just creates a new monetization as a map of params. */
init := func() {
	return ll.toStrict({
		enabled: false,

		// product name of monetization, e.g. "mixcr_analyze"
		productName: "",

		// file name to the param, e.g. {fileName: { arg: "dataset", collect: ["size", "lines", "sha256"] }}
		fileParams: {}
	})
}

fileParamsSchema := {
	"arg": "string",
	"collect": [`string,regex=size|lines|sha256`]
}

schema := {
	"enabled": "bool",
	"productName": "string",
	"fileParams": {"any": fileParamsSchema}
}

/** Same as validation.assertJsonSchema(params, fileParamsSchema, ...),
 * but with prettier errors for clients. */
_validateParams := func(params) {
	ll.assert(ll.isMap(params), "monetization.validateParams(): mnz params must be a map")
	ll.assert(is_string(params.arg), "monetization.validateParams(): 'arg' in {\"mnz\": {...}} param must be a string")
	ll.assert(is_array(params.collect) && len(params.collect) > 0, "monetization.validateParams(): 'collect' in {\"mnz\": {...}} param must be non-empty an array")
}

_validateIsReady := func(mnz) {
	validation.assertJsonSchema(mnz, schema,
		"monetization.assertIsReady(): monetization does not fit schema")
	ll.assert(len(mnz.productName) > 0,
		"monetization.assertIsReady(): product name must be set")
	ll.assert(len(mnz.fileParams) > 0,
		"monetization.assertIsReady(): at least 1 file param must be set")
}

/** Enables the monetization for a product. */
enable := func(mnz, productName) {
	ll.assert(is_string(productName),
		"monetization.enable(): product name must be a string, got %v", productName)

	validation.assertJsonSchema(mnz, schema,
		"monetization.enable(): mnz object does not fit schema")

	mnz.enabled = true
	mnz.productName = productName
}

/** Is this monetization enabled and should run? */
shouldRun := func(mnz) {
	if mnz.enabled {
		_validateIsReady(mnz)
		return true
	}

	return false
}

toJson := func(mnz) {
	validation.assertJsonSchema(mnz, schema,
		"monetization.toJson(): monetization object does not fit json schema")

	return ll.fromStrict(mnz)
}

/** Adds monetization parameters by a file. */
addFileParams := func(mnz, fileName, params) {
	validation.assertJsonSchema(mnz, schema,
		"monetization.addFileParams(): monetization object does not fit json schema")

	_validateParams(params)

	validation.assertJsonSchema(params, fileParamsSchema,
		"monetization.addFileParams(): file params does not fit json schema")

	ll.assert(mnz.enabled,
		"monetization.addFileParams(): monetization must be explicitly enabled by enableMnz(productName) function")

	ll.assert(is_string(fileName),
		"monetization.addFileParams(): file name must be a string.")

	mnz.fileParams[fileName] = params
}

/**
 * Runs monetization binary on a workdir with all files, and returns a new workdir and a resulted jwt token.
 * All resources are ephemeral,
 * the deduplication should be above this level.
 *
 * @param: mnz: monetization -- see a beginning of this file.
 * @param: wd: workdir -- workdir where all files from mnz.fileParams already exist. */
run := func(mnz, wd, queue) {
	_validateIsReady(mnz)

	mnzSw := assets.importSoftware("@platforma-open/milaboratories.software-small-binaries:mnz-client")

	builder := runcmd.builder(wd)

	exprType := feats.commandExpressions ? constants.ARG_TYPE_EXPRESSION : constants.ARG_TYPE_VAR

	builder.envTyped("MI_LICENSE", exprType, "{secrets.MI_LICENSE}")

	opts := internal.swToRunCmdOptions(mnzSw.name, mnzSw.version, mnzSw.descriptor)
	// load pkg var that points to mnz-client location.
	for _, varInfo in opts.variables {
		builder.ref(varInfo.refName, varInfo.reference)
		builder.substitutionRule(varInfo.varName, varInfo.refName)
	}

	builder.cmdTyped(exprType, opts.cmd.value)
	for _, arg in opts.args {
		// now mnz-client doesn't have additional args; this line is just in case.
		builder.argTyped(exprType, arg)
	}
	builder.arg("-productName").arg(mnz.productName)

	// load file params
	for fileName, param in mnz.fileParams {
		// param is of fileParamsSchema type
		toCollect := text.join(param.collect, ",")
		arg := fmt.sprintf("%s:file:%s:%s", param.arg, fileName, toCollect)
		builder.arg(arg)
	}

	builder.inQueue(queue)
	stdout := "mnz_stdout.txt"
	builder.stdout(stdout)
	run := builder.build()

	// save mnz_stdout.txt
	savedWd := workdir.save(run.workdir).
		saveFileContent(stdout).
		build()

	jwtResultAsBytes := savedWd.filesContent.getFutureInputField(stdout)

	// runcmd refs gets only json/objects as values,
	// but workdir save returns binary:1 resource type.
	// Hence here we have to convert it to the json's string.
	jwtResultAsString := render.create(
		assets.importTemplate(":exec.monetization_to_json"),
		{ resource: jwtResultAsBytes }
	).output("jsonedData")

	return ll.toStrict({
		// other commands should run on this wd,
		// or else they will run concurrently
		// with monetization.
		wdAfter: savedWd.workdir,

		// mnz-client returns jwt token
		// that we will pass to the next command as env.
		// If we need to change env to a file, that's the place.
		jwtEnvName: _JWT_TOKEN_RESULT_ENV,
		jwtEnvValue: jwtResultAsString
	})
}

export ll.toStrict({
	init: init,
	schema: schema,
	enable: enable,
	shouldRun: shouldRun,
	toJson: toJson,
	addFileParams: addFileParams,
	run: run
})
