// exec.tpl

/**
 * Creates and fill a workdir, runs a command on it
 * and saves a chosen resulted files from this dir.
 */

self := import(":tpl")

ll := import(":ll")
smart := import(":smart")
render := import(":render")
workdir := import(":workdir")
runcmd := import(":exec.runcmd")
stream := import(":exec.stream")
execInternal := import(":exec.internal")
software := import(":software")
sets := import(":sets")
validation := import(":validation")

self.defineOutputs(
	"files", "filesContent", "fileSets", "fileSetsContent", "progress", "fileStreams", "wdProcessors"
)

_runBinaryCommand := func(inputs) {
	//
	// Create and fill the workdir with files that were passed from inputs.
	//

	wdBuilder := workdir.builder()
	// add files
	wdBuilder.addFiles(inputs.filesToAdd)
	// create dirs
	for dir in inputs.dirsToCreate {
		wdBuilder.mkDir(dir)
	}
	// pass values as fields for saving the topology in pl-core
	// and avoiding deduplication issues:
	wdBuilder.writeFiles(self.rawInputs().filesToWrite.getValue().inputs())
	// working directory with input files added; before run
	wdBefore := wdBuilder.build()

	//
	// Build runcmd resource
	//

	runBuilder := runcmd.builder(wdBefore)
	
	opts := execInternal.binaryRunOptions(inputs.runOptions)

	// Attach installed software to the command
	for pkgInfo in opts.packages {
		runBuilder.ref(pkgInfo.name, pkgInfo.reference)
	}

	// Patch command's PATH env variable
	for p in opts.customPaths {
		runBuilder.addToPath(p.key, p.value)
	}

	// exec command
	if opts.cmd.isRef {
		runBuilder.cmdExecutable(opts.cmd.key, opts.cmd.value)
	} else {
		runBuilder.cmd(opts.cmd.value)
	}

	// cmd args
	for name, arg in opts.args {
		if arg.isRef {
			runBuilder.argExecutable(arg.key, arg.value)
		} else {
			runBuilder.arg(arg.value)
		}
	}

	// env vars
	for name, value in opts.envs {
		runBuilder.env(name, value)
	}
	// other options
	runBuilder.inQueue(opts.queue)
	runBuilder.stdout(opts.stdout)
	runBuilder.stderr(opts.stderr)
	runBuilder.nErrorLines(opts.nErrorLines)
	run := runBuilder.build()
	// get the workdir containing all the files, including produced by the run command
	wdAfter := run.workdir

	//
	// Run workdir processors
	//

	wdProcessorsResults := {}
	for name, p in inputs.wdProcessors {
		tpl := p.tpl
		tplInputs := {
			workdir: wdAfter,
			args: p.tplArgs
		}

		// render template & get results
		r := render.createEphemeral(tpl, tplInputs)

		wdProcessorsResults[name] = r.output("result")
		wdAfter = r.output("workdir")
	}

	//
	// Save data from the workdir
	//

	// saver resource
	wdSaver := workdir.save(wdAfter)
	// save files
	for fName in inputs.filesToSave {
		wdSaver.saveFile(fName)
	}
	// save files content
	for fName in inputs.filesContentToSave {
		wdSaver.saveFileContent(fName)
	}
	// save file sets
	for name, regex in inputs.fileSetsToSave {
		wdSaver.saveFileSet(name, regex)
	}
	// save file sets content
	for name, regex in inputs.fileSetsContentToSave {
		wdSaver.saveFileSetContent(name, regex)
	}

	// resulting wd
	wdResult := wdSaver.build()

	// create streams for all requested file streams.
	streams := smart.mapBuilder()
	for fName in inputs.filesToStream {
		streams.add(fName, stream.create(wdBefore, wdResult, fName))
	}

	return {
		files: wdResult.files,
		filesContent: wdResult.filesContent,
		fileSets : wdResult.fileSets,
		fileSetsContent : wdResult.fileSetsContent,
		fileStreams: streams.build(),
		wdProcessors: wdProcessorsResults,
		progress: wdResult.progress
	}
}

self.body(func(inputs) {
	// assert we have inputs we expect.
	cmdScheme := {
		isRef: "bool",
		value: "string"
	}

	validation.assertJsonSchema(inputs, {
		"softwareInfo,omitempty": validation.resource,
		files: { any: validation.resource },
		values: { any: "any" },
		dirs: ["string"],
		runOptions: {
			"software,omitempty": software.infoScheme,
			"cmd,omitempty": "string",
			"args,omitempty": [cmdScheme],
			envs: { any: "string" },
			queue: "string",
			stdout: "string",
			stderr: "string",
			nErrorLines: "number"
		},
		saveFiles: ["string"],
		saveFilesContents: ["string"],
		streams: ["string"]
	})

	return _runBinaryCommand(inputs)
})
