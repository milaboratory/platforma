/**
 * A builder for executing commands.
 */

ll := import(":ll")
validation := import(":validation")

assets := import(":assets")
smart := import(":smart")
times := import("times")
maps := import(":maps")
sets := import(":sets")
slices := import(":slices")
path := import(":path")
render := import(":render")
enum := import("enum")
oop := import(":oop")
constants := import(":constants")
monetization := import(":exec.monetization_internal")
execConstants := import(":exec.constants")

/**
 * Creates a new builder for constructing and executing commands.
 *
 * @return builder - a new builder instance.
 */
builder := func() {
	self := undefined

	soft := undefined
	cmd := undefined
	args := [] // list of strings
	argsTyped := [] // list of {type: expression|string, value: string}
	typedArgSet := false
	typedEnvSet := false
	envs := {} // map of names to values-strings
	envsTyped := {} // map of names to {type: expression|string, value: string}
	secrets := {} // { "env_name": "{secrets.secret_name}", ... }

	entrypointSet := false

	assertEntrypointNotSet := func() {
		ll.assert(!entrypointSet,
			"either a command with arguments or a software were already set.")
	}

	//
	// Limits
	//
	queue := execConstants.HEAVY_QUEUE
	cpuRequest := undefined
	ramRequest := undefined

	cache := 0
	inputCache := 0
	nErrorLines := 200

	// files to be added in workdir
	filesToAdd := {}
	// files to be created in the workdir with the content from resources
	filesToWrite := {}
	// directories to create in the workdir
	dirsToCreate := {}

	// files to save from the workdir
	filesToSave := {}
	// files which content to save from the workdir
	filesContentToSave := {}
	// file sets to save by regex
	fileSetsToSave := {}
	// file sets to save by regex (save file content)
	fileSetsContentToSave := {}
	// files to stream from the workdir
	filesToStream := {}
	// workdir processors
	wdProcessors := {}

	// assets to be extracted into workdir before execution
	assetInfos := {}
	assetDescriptors := {}
	assetRules := {}
	assetCache := {}

	mnz := monetization.init()

	stdout := "stdout.txt"
	stderr := "stderr.txt"
	stderrToStdout := false
	stdoutFile := false
	stderrFile := false
	stdoutStream := false
	stderrStream := false
	stdoutContent := false
	stderrContent := false

	self = ll.toStrict({
		/**
		 * Sets the command to be executed, e.g. /usr/bin/env or sh.
		 *
		 * @param commandName: string - the name of the command.
		 */
		cmd: func(commandName) {
			assertEntrypointNotSet()

			cmd = commandName
			entrypointSet = true
			return self
		},

		/**
		 * Sets the software to be executed.
		 *
		 * @param software: softwareInfo - the software to execute.
		 */
		software: func(sw) {
			assertEntrypointNotSet()
			ll.assert(assets.isSoftwareInfo(sw),
				"exec.builder.software: <software> parameter is not a software info. Provide the value obtained from assets.importSoftware() here"
			)

			soft = sw
			entrypointSet = true
			return self
		},

		/**
		 * Adds an argument to the command.
		 *
		 * @param arg: string - the argument to add.
		 */
		arg: func(arg) {
			ll.assert(is_string(arg), "exec.arg(): argument must be a string, got: %v", arg)
			args = append(args, arg)
			argsTyped = append(argsTyped, {
				type: execConstants.ARG_TYPE_STRING,
				value: arg
			})
			return self
		},

		/**
		 * Adds an argument with expression evaluation.
		 *
		 * Expressions are evaluated using the expr-lang syntax (https://expr-lang.org/docs/language-definition).
		 * Available variables in expressions:
		 *
		 * - `secrets.SECRET_NAME` - Access to secrets (e.g., `{secrets.API_KEY}`)
		 * - `env.ENV_NAME` - Access to environment variables (automatically converted to appropriate types)
		 * - `system.cpu` - Number of CPU cores allocated
		 * - `system.ram.bytes`, `system.ram.b` - RAM in bytes
		 * - `system.ram.kb`, `system.ram.mb`, `system.ram.gb` - RAM in metric units (base-10)
		 * - `system.ram.kib`, `system.ram.mib`, `system.ram.gib` - RAM in binary units (base-2)
		 * - Custom substitution variables defined by substitution rules
		 *
		 * Expression examples:
		 * - `--memory={system.ram.mb * 80 / 100}` - Calculate 80% of available RAM in MB
		 * - `--java-home={env.JAVA_HOME}` - Use environment variable
		 * - `--threads={system.cpu}` - Use number of CPU cores
		 * - `--config={secrets.CONFIG_PATH}` - Access secret value
		 *
		 * @param arg: string - argument with expressions, e.g., "-Xms{system.ram.mb * 80 / 100}m"
		 */
		argExpr: func(arg) {
			ll.assert(is_string(arg), "exec.argExpr(): argument must be a string, got: %v", arg)
			argsTyped = append(argsTyped, {
				type: execConstants.ARG_TYPE_EXPRESSION,
				value: arg
			})
			typedArgSet = true

			return self
		},

		/**
		 * @deprecated: use argExpr() instead. This method will be removed after 15 Mar 2025.
		 * Adds an argument with expression evaluation.
		 *
		 * @param arg: string - argument with expressions, e.g., "-Xms{system.ram.mb * 80 / 100}m"
		 */
		argWithVar: func(arg) {
			return self.argExpr(arg)
		},

		/**
		 * Sets an environment variable.
		 *
		 * @param name: string - the name of the environment variable.
		 * @param value: string - the value of the environment variable.
		 */
		env: func(name, value) {
			validation.assertType([name, value], ["string"])
			envs[name] = value
			envsTyped[name] = {
				type: execConstants.ARG_TYPE_STRING,
				value: value
			}
			return self
		},

		/**
		 * Sets an environment variable with expression evaluation.
		 *
		 * Expressions are evaluated using the expr-lang syntax (https://expr-lang.org/docs/language-definition).
		 * Available variables in expressions:
		 *
		 * - `secrets.SECRET_NAME` - Access to secrets (e.g., `{secrets.API_KEY}`)
		 * - `env.ENV_NAME` - Access to environment variables (automatically converted to appropriate types)
		 * - `system.cpu` - Number of CPU cores allocated
		 * - `system.ram.bytes`, `system.ram.b` - RAM in bytes
		 * - `system.ram.kb`, `system.ram.mb`, `system.ram.gb` - RAM in metric units (base-10)
		 * - `system.ram.kib`, `system.ram.mib`, `system.ram.gib` - RAM in binary units (base-2)
		 * - Custom substitution variables defined by substitution rules
		 *
		 * Expression examples:
		 * - `{env.JAVA_HOME}/bin/java` - Use environment variable
		 * - `{system.ram.mb * 80 / 100}` - Calculate 80% of available RAM in MB
		 * - `{secrets.DATABASE_URL}` - Access secret value
		 * - `{system.cpu}` - Use number of CPU cores
		 *
		 * @param name: string - the name of the environment variable.
		 * @param value: string - the value with expressions, e.g., "-Xms{system.ram.mb * 80 / 100}m"
		 */
		envExpr: func(name, value) {
			validation.assertType([name, value], ["string"])
			envsTyped[name] = {
				type: execConstants.ARG_TYPE_EXPRESSION,
				value: value
			}
			typedEnvSet = true

			return self
		},

		/**
		 * @deprecated: use envExpr() instead. This method will be removed after 15 Mar 2025.
		 * Sets an environment variable with expression evaluation.
		 *
		 * @param name: string - the name of the environment variable.
		 * @param value: string - the value with expressions, e.g., "-Xms{system.ram.mb * 80 / 100}m"
		 */
		envWithVar: func(name, value) {
			return self.envExpr(name, value)
		},

		/**
		 * Sets multiple environment variables.
		 *
		 * @param envMap: map[string]string - a map of environment variables.
		 */
		envMap: func(envMap) {
			enum.each(envMap, self.env)
			return self
		},

		/**
		 * Set environment variable to value of given secret.
		 *
		 * @param secretName: string - name of secret to pass as environment variable.
		 * @param envName: string - name of environment variable to set with value of secret.
		 */
		secret: func(secretName, envName) {
			validation.assertType([secretName, envName], ["string"])
			secrets[envName] = "{secrets."+secretName+"}"
			return self
		},

		/**
		 * Sets the queue to execute the command in.
		 *
		 * In local execution mode, the platform distributes available host RAM among four predefined queues:
		 * 'heavy', 'medium', 'light', and 'ui-tasks'. This allows managing resource consumption for concurrent tasks.
		 * The RAM allocation is dynamic and depends on the total system memory, with different allocation strategies
		 * for different hardware configurations.
		 *
		 * Each queue has a memory limit, which represents the absolute maximum memory a queue can use.
		 *
		 * Here are a couple of examples of how memory is distributed:
		 *
		 * **For a system with 32GiB of available RAM:**
		 * - `ui-tasks`:      Memory Limit ~9.9GiB
		 * - `light`:         Memory Limit ~10.5GiB
		 * - `medium`:        Memory Limit ~12.7GiB
		 * - `heavy`:         Memory Limit ~23.5GiB
		 *
		 * **For a system with 64GiB of available RAM:**
		 * - `ui-tasks`:      Memory Limit ~12.6GiB
		 * - `light`:         Memory Limit ~16.0GiB
		 * - `medium`:        Memory Limit ~21.4GiB
		 * - `heavy`:         Memory Limit ~39.1GiB
		 *
		 * **For a system with 256GiB of available RAM:**
		 * - `ui-tasks`:      Memory Limit ~36.2GiB
		 * - `light`:         Memory Limit ~51.0GiB
		 * - `medium`:        Memory Limit ~72.8GiB
		 * - `heavy`:         Memory Limit ~156.0GiB
		 *
		 * If a task is submitted without specifying memory requirements, it receives a default amount of RAM based on its queue:
		 * - `heavy`: 32GiB
		 * - `medium`: 1GiB
		 * - `light`: 512MiB
		 * - `ui-tasks`: 100MiB
		 *
		 * **Important Note:** The queue behavior and memory limits described above apply specifically to **local execution**.
		 * When using a batch executor (such as Slurm, Google Cloud Batch, or AWS Batch), the interpretation of queue names
		 * and the enforcement of memory limits are determined by the configuration of the specific execution backend.
		 * Furthermore, if you request more memory than a queue can provide, your request will be capped at the queue's limit.
		 * For example, requesting 100GiB in `ui-tasks` on a 32GiB system will result in an allocation of ~9.9GiB.
		 *
		 * Available queues are:
		 * - execConstants.HEAVY_QUEUE ('heavy') - default
		 * - execConstants.MEDIUM_QUEUE ('medium')
		 * - execConstants.LIGHT_QUEUE ('light')
		 * - execConstants.UI_TASKS_QUEUE ('ui-tasks')
		 *
		 * @param newQueue: string - the name of the queue.
		 */
		setQueue: func(newQueue) {
			queue = newQueue
			return self
		},

		/**
		 * Execute command in the 'heavy' queue.
		 */
		inHeavyQueue: func() {
			queue = execConstants.HEAVY_QUEUE
			return self
		},

		/**
		 * Execute command in the 'medium' queue.
		 */
		inMediumQueue: func() {
			queue = execConstants.MEDIUM_QUEUE
			return self
		},

		/**
		 * Execute command in the 'light' queue.
		 */
		inLightQueue: func() {
			queue = execConstants.LIGHT_QUEUE
			return self
		},

		/**
		 * Execute command in the 'ui-tasks' queue.
		 */
		inUiQueue: func() {
			queue = execConstants.UI_TASKS_QUEUE
			return self
		},

		/**
		 * Sets the number of CPUs to request from the underlying executor (i.e. Google/AWS Batch, PBS, Slurm, etc.).
		 *
		 * @param amount: number - number of cores requested for command.
		 */
		cpu: func(amount) {
			validation.assertType(amount, "number", "exec.builder.cpu: amount of cores must be a number")
			ll.assert(amount > 0, "exec.builder.cpu: amount of cores should be greater than 0")
			cpuRequest = amount

			return self
		},

		/**
		 * Sets the amount of RAM to request from the underlying executor (i.e. Google/AWS Batch, PBS, Slurm, etc.).
		 *
		 * @param amount: number | string - amount of RAM in bytes or string with size suffix (case-insensitive):
		 *                                     K,  KB,  M,  MB,  G,  GB for base-10 sizes (x1000)
		 *                                    Ki, KiB, Mi, MiB, Gi, GiB for base-2 sizes (x1024)
		 *                                  when operating with bytes, you may use 'units' package for convenience:
		 *                                    120 * units.GiB
		 *
		 * @return builder
		 */
		mem: func(amount) {
			validation.assertType(amount, ["or", "number", "string"], "exec.builder.mem: RAM amount should be a number or string")
			ll.assert(is_string(amount) || amount > 0, "exec.builder.mem: amount in bytes should be greater than 0")
			ramRequest = amount

			return self
		},

		/**
		 * @deprecated: use mem() instead.
		 * Deprecated: use mem() instead.
		 *
		 * @return builder
		 */
		ram: func(amount) {
			self.mem(amount)
			return self
		},

		/**
		 * Use cacheMillis, cacheSeconds, cacheMinutes, cacheHours and cacheDays to not make hidden mistakes in cache time ranges.
		 * Sets the cache time.
		 *
		 * @param time: duration - the cache time from 'times' library.
		 */
		cache: func(time) {
			ll.assert(
				is_int(time),
				"cache time must be an integer. " +
					"Did you forget to import a standard tengo library 'times'?")
			cache = time
			return self
		},

		/**
		 * Sets the cache time in milliseconds.
		 *
		 * @param millis: number
		 */
		cacheMillis: func(millis) {
			ll.assert(is_int(millis) && millis > 0, "cache time must be a number of milliseconds")
			cache = millis * times.millisecond
			return self
		},

		/**
		 * Sets the cache time in seconds.
		 *
		 * @param seconds: number
		 */
		cacheSeconds: func(seconds) {
			ll.assert(is_int(seconds) && seconds > 0, "cache time must be a number of seconds")
			cache = seconds * times.second
			return self
		},

		/**
		 * Sets the cache time in minutes.
		 *
		 * @param minutes: number
		 */
		cacheMinutes: func(minutes) {
			ll.assert(is_int(minutes) && minutes > 0, "cache time must be a number of minutes")
			cache = minutes * times.minute
			return self
		},

		/**
		 * Sets the cache time in hours.
		 *
		 * @param hours: number
		 */
		cacheHours: func(hours) {
			ll.assert(is_int(hours) && hours > 0, "cache time must be a number of hours")
			cache = hours * times.hour
			return self
		},

		/**
		 * Sets the cache time in days
		 *
		 * @param days: number
		 */
		cacheDays: func(days) {
			ll.assert(is_int(days) && days > 0, "cache time must be a number of days")
			cache = days * times.hour * 24
			return self
		},

		/**
		 * Sets the cache duration for the execution's inputs.
		 *
		 * This is useful for an execution that is part of a sequence that might be re-rendered soon.
		 * Caching allows the inputs to be reused by the recovery mechanism, avoiding the need to
		 * recalculate them. A duration of one minute is recommended.
		 *
		 * @param time: duration - the cache time from 'times' library.
		 */
		cacheInputs: func(time) {
			ll.assert(
				is_int(time),
				"input cache time must be an integer. " +
					"Did you forget to import a standard tengo library 'times'?")
			inputCache = time
			return self
		},

		/**
		 * Sets the input cache time in milliseconds.
		 *
		 * @param millis: number
		 */
		cacheInputsMillis: func(millis) {
			ll.assert(is_int(millis) && millis > 0, "input cache time must be a number of milliseconds")
			inputCache = millis * times.millisecond
			return self
		},

		/**
		 * Sets the input cache time in seconds.
		 *
		 * @param seconds: number
		 */
		cacheInputsSeconds: func(seconds) {
			ll.assert(is_int(seconds) && seconds > 0, "input cache time must be a number of seconds")
			inputCache = seconds * times.second
			return self
		},

		/**
		 * Sets the input cache time in minutes.
		 *
		 * @param minutes: number
		 */
		cacheInputsMinutes: func(minutes) {
			ll.assert(is_int(minutes) && minutes > 0, "input cache time must be a number of minutes")
			inputCache = minutes * times.minute
			return self
		},

		/**
		 * Sets the input cache time in hours.
		 *
		 * @param hours: number
		 */
		cacheInputsHours: func(hours) {
			ll.assert(is_int(hours) && hours > 0, "input cache time must be a number of hours")
			inputCache = hours * times.hour
			return self
		},

		/**
		 * Sets the input cache time in days
		 *
		 * @param days: number
		 */
		cacheInputsDays: func(days) {
			ll.assert(is_int(days) && days > 0, "input cache time must be a number of days")
			inputCache = days * times.hour * 24
			return self
		},

		/**
		 * Sets the number of error lines to be captured.
		 *
		 * @param lines: number - the number of error lines.
		 */
		nErrorLines: func(lines) {
			ll.assert(
				is_int(lines) && lines >= 0,
				"lines must be integer")

			nErrorLines = lines
			return self
		},

		/**
		 * Adds files from given asset into working directory.
		 *
		 * @param asset: assetInfo - asset info loaded from assets.importAsset()
		 * @param destinationDir: string - path inside working directory for files to be extracted from archive. Use "." for root of the workdir.
		 * @param pathsInArchive: string[] - paths inside the archive to be extracted into destinationDir.
		 *                                   Empty array means 'unpack full archive'.
		 */
		addAsset: func(asset, destinationDir, ...pathsInArchive) {
			if (len(pathsInArchive) == 1 && is_array(pathsInArchive[0])) {
				// support [<paths>] single argument
				pathsInArchive = pathsInArchive[0]
			}
			if len(pathsInArchive) == 0 {
				// default for 'no paths' is to extract all
				pathsInArchive = []
			}

			ll.assert(assets.isAssetInfo(asset),
				"exec.builder.addAsset: <asset> parameter is not an asset info. Provide the value obtained from assets.importAsset() here")
			validation.assertType(destinationDir, "string", "exec.builder.addAsset: <destinationDir> is not a string. It must be a path inside working directory")
			validation.assertType(pathsInArchive, ["string"], "exec.builder.addAsset: <pathsInArchive> must contain list of strings with paths inside archive")

			assetIdKey := ll.idToString(asset._id)
			if is_undefined(assetRules[assetIdKey]) {
				assetInfos[assetIdKey] = asset.resource
				assetDescriptors[assetIdKey] = ll.fromStrict(asset.descriptor)
				assetRules[assetIdKey] = {}
			}

			if len(pathsInArchive) == 0 || is_undefined(assetRules[assetIdKey][destinationDir]) {
				assetRules[assetIdKey][destinationDir] = []
			}

			if (len(pathsInArchive) > 0) {
				assetRules[assetIdKey][destinationDir] = append(assetRules[assetIdKey][destinationDir], pathsInArchive...)
			}

			return self
		},

		/**
		 * Set custom cache time for given asset. By default, cache time of 1 day is applied to all assets.
		 * This allows to customize cache time on per-asset basis
		 */
		cacheAssetMillis: func (asset, millis) {
			ll.assert(assets.isAssetInfo(asset),
				"exec.builder.cacheAssetMillis: <asset> parameter is not an asset info. Provide the value obtained from assets.importAsset() here")
			ll.assert(is_int(millis),
				"exec.builder.cacheAssetMillis: asset cache time must be a number of milliseconds.")

			assetIdKey := ll.idToString(asset._id)
			assetCache[assetIdKey] = millis * times.millisecond

			return self
		},

		/**
		 * Set custom cache time for given asset. By default, cache time of 1 day is applied to all assets.
		 * This allows to customize cache time on per-asset basis
		 */
		cacheAssetSeconds: func (asset, seconds) {
			ll.assert(assets.isAssetInfo(asset),
				"exec.builder.cacheAssetSeconds: <asset> parameter is not an asset info. Provide the value obtained from assets.importAsset() here")
			ll.assert(is_int(seconds),
				"exec.builder.cacheAssetSeconds: asset cache time must be a number of seconds.")

			assetIdKey := ll.idToString(asset._id)
			assetCache[assetIdKey] = seconds * times.second

			return self
		},

		/**
		 * Set custom cache time for given asset. By default, cache time of 1 day is applied to all assets.
		 * This allows to customize cache time on per-asset basis
		 */
		cacheAssetMinutes: func (asset, minutes) {
			ll.assert(assets.isAssetInfo(asset),
				"exec.builder.cacheAssetMinutes: <asset> parameter is not an asset info. Provide the value obtained from assets.importAsset() here")
			ll.assert(is_int(minutes),
				"exec.builder.cacheAssetMinutes: asset cache time must be a number of minutes.")

			assetIdKey := ll.idToString(asset._id)
			assetCache[assetIdKey] = minutes * times.minute

			return self
		},

		/**
		 * Set custom cache time for given asset. By default, cache time of 1 day is applied to all assets.
		 * This allows to customize cache time on per-asset basis
		 */
		cacheAssetHours: func (asset, hours) {
			ll.assert(assets.isAssetInfo(asset),
				"exec.builder.cacheAssetHours: <asset> parameter is not an asset info. Provide the value obtained from assets.importAsset() here")
			ll.assert(is_int(hours),
				"exec.builder.cacheAssetHours: asset cache time must be a number of hours.")

			assetIdKey := ll.idToString(asset._id)
			assetCache[assetIdKey] = hours * times.hour

			return self
		},

		/**
		 * Set custom cache time for given asset. By default, cache time of 1 day is applied to all assets.
		 * This allows to customize cache time on per-asset basis
		 */
		cacheAssetDays: func (asset, days) {
			ll.assert(assets.isAssetInfo(asset),
				"exec.builder.cacheAssetHours: <asset> parameter is not an asset info. Provide the value obtained from assets.importAsset() here")
			ll.assert(is_int(days),
				"exec.builder.cacheAssetHours: asset cache time must be a number of days.")

			assetIdKey := ll.idToString(asset._id)
			assetCache[assetIdKey] = days * 24 * times.hour

			return self
		},

		enableMnz: func(productKey) {
			monetization.enable(mnz, productKey)
			// exec supports only 1 run.
			monetization.appendRun(mnz)

			return self
		},

		setMnzUrl: func(url) {
			monetization.setUrl(mnz, url)
			return self
		},

		/**
		 * Adds a file to the working directory.
		 * When called with the same fileName multiple times,
		 * the last call will overwrite the previous ones.
		 *
		 * @param fileName: string - the name of the input file.
		 * @param file: reference - a resource id of the file or a field that points to it.
		 */
		addFile: func(fileName, file, ...opts) {
			ll.assert(is_string(fileName), "exec.builder().addFile: fileName must be a string")
			ll.assert(smart.isReference(file), "exec.builder().addFile: file must be a reference")

			fileName = path.canonize(fileName)
			filesToAdd[fileName] = file
			monetization.addOptionalArg(mnz, fileName, opts...)
			return self
		},

		/**
		 * Adds multiple files to the working directory.
		 *
		 * @param filesMap: map[string]reference - a map of file names to files' references.
		 */
		addFiles: func(filesMap, ...opts) {
			ll.assert(ll.isMap(filesMap), "exec.builder().addFiles: filesMap must be map of file names to file resources")
			maps.forEach(filesMap, func(fileName, file) {
				self.addFile(fileName, file, opts...)
			})
			return self
		},

		/**
		 * Writes a file with a given content.
		 * When called with the same fileName multiple times,
		 * the last call will overwrite the previous ones.
		 *
		 * @param fileName: string - the name of the content.
		 * @param data: bytes|reference - a primitive tengo value (map or array) or a reference to
		 *        a resource from which data we create a file.
		 */
		writeFile: func(fileName, data, ...opts) {
			validation.assertType(data, ["or",
				"string",
				"bytes",
				validation.reference])

			fileName = path.canonize(fileName)

			filesToWrite[fileName] = data
			monetization.addOptionalArg(mnz, fileName, opts...)

			return self
		},

		/**
		 * Creates a directory.
		 *
		 * @param dir: string - the directory to create.
		 */
		mkDir: func(dir) {
			dir = path.canonize(dir)
			dirsToCreate = sets.add(dirsToCreate, dir)
			return self
		},

		/**
		 * Saves a file.
		 *
		 * @param fileName: string - the name of the output file.
		 */
		saveFile: func(fileName) {
			fileName = path.canonize(fileName)
			sets.add(filesToSave, fileName)
			return self
		},

		/**
		 * Saves a file's content to a value resource.
		 *
		 * @param fileName: string - the name of the output value.
		 */
		saveFileContent: func(fileName) {
			fileName = path.canonize(fileName)
			sets.add(filesContentToSave, fileName)
			return self
		},

		/**
		 * Saves files from the exec workdir using regex.
		 *
		 * @param name: string - a name to refer this file set in the outputs
		 * @param regex: string - regex pattern
		 */
		saveFileSet: func(name, regex) {
			fileSetsToSave[name] = regex
			return self
		},

		/**
		 * Run specified
		 */
		processWorkdir: func(name, tpl, tplArgs) {
			wdProcessors[name] = {
				tpl: tpl,
				tplArgs: tplArgs
			}
			return self
		},

		/**
		 * Saves files content from the exec workdir using regex.
		 *
		 * @param name: string - a name to refer this file set in the outputs
		 * @param regex: string - regex pattern
		 */
		saveFileSetContent: func(name, regex) {
			fileSetsContentToSave[name] = regex
			return self
		},

		/**
		 * A command to stream a file by a given file name.
		 *
		 * @param fileName: string - the name of the file to stream.
		 */
		streamFile: func(fileName) {
			fileName = path.canonize(fileName)
			sets.add(filesToStream, fileName)

			return self
		},

		/**
		 * Set stdout file name
		 */
		stdoutFileName: func(fileName) {
			fileName = path.canonize(fileName)
			stdout = fileName
			return self
		},

		/**
		 * Set stderr file name
		 */
		stderrFileName: func(fileName) {
			fileName = path.canonize(fileName)
			stderr = fileName
			return self
		},

		/**
		 * Sets stdout to be captured as a value resource.
		 */
		saveStdoutContent: func() {
			stdoutContent = true
			return self
		},

		/**
		 * Sets stderr to be captured as a value resource.
		 */
		saveStderrContent: func() {
			stderrContent = true
			return self
		},

		/**
		 * Redirects stderr output to stdout.
		 */
		printErrStreamToStdout: func() {
			stderrToStdout = true
			return self
		},

		/**
		 * Save stdout as file.
		 */
		saveStdoutFile: func() {
			stdoutFile = true
			return self
		},

		/**
		 * Save stderr as file.
		 */
		saveStderrFile: func() {
			stderrFile = true
			return self
		},

		/**
		 * Save stdout as stream.
		 */
		saveStdoutStream: func() {
			stdoutStream = true
			return self
		},

		/**
		 * Save stderr as stream.
		 */
		saveStderrStream: func() {
			stderrStream = true
			return self
		},

		/**
		 * Executes the command.
		 *
		 * @return A map containing the execution results.
		 */
		run: func() {
			if stderrToStdout {
				stderr = stdout
			}
			if stdoutFile {
				sets.add(filesToSave, stdout)
			}
			if stderrFile {
				sets.add(filesToSave, stderr)
			}
			if stdoutStream {
				sets.add(filesToSave, stdout)
				sets.add(filesToStream, stdout)
			}
			if stderrStream {
				sets.add(filesToSave, stderr)
				sets.add(filesToStream, stderr)
			}
			if stdoutContent {
				sets.add(filesContentToSave, stdout)
			}
			if stderrContent {
				sets.add(filesContentToSave, stderr)
			}

			// We don't want these values to be encoded as JSON's since
			// values are pure bytes.
			// Thus, we create a resource with a custom type here manually.
			filesToWriteRefs := maps.mapValues(filesToWrite, func(v) {
				if smart.isReference(v) { return v }
				return smart.createValueResource(constants.RTYPE_BINARY_VALUE, v)
			})

			ll.assert(entrypointSet,
				"incomplete command: cmd() or software() must be provided to exec.builder")

			runOptions := {
				cmd: cmd,
				args: args,
				envs: envs,
				secrets: secrets,
				queue: queue,
				// both stdout and stderr will always be channeled to a file
				// even if stdoutFile or stderrFile are false
				stdout: stdout,
				stderr: stderr,
				nErrorLines: nErrorLines
			}

			pureExecInputs := {
				filesToAdd: smart.createMapResource(filesToAdd),
				filesToWrite: smart.createMapResource(filesToWriteRefs),
				dirsToCreate: slices.fromSet(dirsToCreate),
				runOptions: runOptions,
				filesToSave: slices.fromSet(filesToSave),
				filesContentToSave: slices.fromSet(filesContentToSave),
				fileSetsToSave: fileSetsToSave,
				fileSetsContentToSave: fileSetsContentToSave,
				filesToStream: slices.fromSet(filesToStream),
				wdProcessors: wdProcessors,
				assets: smart.createMapResource(assetInfos),
				assetDescriptors: assetDescriptors,
				assetRules: assetRules,
				assetCache: assetCache
			}

			if (is_int(ramRequest)) {
				ramRequest = string(ramRequest) + "b"
			}
			metaExecInputs := {
				quota: smart.createJsonResource({
					queue: queue,
					cpu: cpuRequest,
					ram: ramRequest
				})
			}

			// We add monetization only if it's enabled
			// to not break a deduplication of exec's
			// that were created before this feature existed.
			if monetization.shouldRun(mnz) {
				pureExecInputs.runOptions.monetization = monetization.toJson(mnz)
			}

			// We add new argsTyped and envsTyped only when they were added
			// to not break a deduplication on old pl backend.
			if typedArgSet || typedEnvSet {
				pureExecInputs.runOptions.argsTyped = argsTyped
				pureExecInputs.runOptions.envsTyped = envsTyped
			}

			if !is_undefined(soft) {
				pureExecInputs.softwareInfo = soft.resource
				pureExecInputs.runOptions.software = {
					name: soft.name,
					version: soft.version,
					descriptor: ll.fromStrict(soft.descriptor)
				}
			}

			renderOpts := { metaInputs: metaExecInputs }
			if inputCache > 0 {
				renderOpts.inputCache = inputCache
			}

			tpl := render.create(assets.importTemplate(":exec.exec"), pureExecInputs, renderOpts)

			self := undefined
			self = ll.toStrict(oop.inherit(tpl, {
				/**
				 * Retrieves a file.
				 *
				 * @param fileName: string
				 * @return field - a reference to the file resource.
				 */
				getFile: func(fileName) {
					ll.assert(sets.hasElement(filesToSave, fileName),
						"file '%s' was not saved, add saveFile() call to the exec builder", fileName)
					return tpl.output("files", cache).getFutureInputField(fileName)
				},

				/**
				 * Retrieves the content of a file.
				 *
				 * @param fileName: string
				 * @return field - a reference to the file content resource.
				 */
				getFileContent: func(fileName) {
					ll.assert(sets.hasElement(filesContentToSave, fileName),
						"file '%s' content was not saved, add saveFileContent() call to the exec builder", fileName)
					return tpl.output("filesContent", cache).getFutureInputField(fileName)
				},

				/**
				 * Retrieves a file stream.
				 *
				 * @param fileName: string
				 * @return field - a reference to the file stream resource.
				 */
				getFileStream: func(fileName) {
					ll.assert(sets.hasElement(filesToStream, fileName),
						"file '%s' stream was not saved, add streamFile() call to the exec builder", fileName)
					return tpl.output("fileStreams", cache).getFutureInputField(fileName, true)
				},

				/**
				 * Retrieves a file set resource.
				 *
				 * @param fileSetName: string - name of a file set
				 * @return field - a reference to the map resource with files
				 */
				getFileSet: func(fileSetName) {
					ll.assert(!is_undefined(fileSetsToSave[fileSetName]),
						"file set '%s' was not saved, add saveFileSet() call to the exec builder", fileSetName)
					return tpl.output("fileSets", cache).getFutureInputField(fileSetName)
				},

				/**
				 * Retrieves a file set content resource.
				 *
				 * @param fileSetName: string - name of a file set
				 * @return field - a reference to the map resource with files
				 */
				getFileSetContent: func(fileSetName) {
					ll.assert(!is_undefined(fileSetsContentToSave[fileSetName]),
						"file set '%s' content was not saved, add saveFileSetContent() call to the exec builder", fileSetName)
					return tpl.output("fileSetsContent", cache).getFutureInputField(fileSetName)
				},

				getProcessorResult: func(procName) {
					ll.assert(!is_undefined(wdProcessors[procName]),
						"processor '%s' result was not saved, add processWorkdir() call to the exec builder", procName)
					return tpl.output("wdProcessors", cache).getFutureInputField(procName)
				},

				/**
				 * Retrieves the stdout stream.
				 */
				getStdoutStream: func() {
					ll.assert(stdoutStream,
						"stdout stream was not saved, add saveStdoutStream() call to the exec builder")
					return self.getFileStream(stdout)
				},

				/**
				 * Retrieves the stderr stream.
				 */
				getStderrStream: func() {
					ll.assert(stderrStream,
						"stderr stream was not saved, add saveStderrStream() call to the exec builder")
					return self.getFileStream(stderr)
				},

				/**
				 * Retrieves the stdout file.
				 */
				getStdoutFile: func() {
					ll.assert(stdoutFile,
						"stdout file was not saved, add saveStdoutFile() call to the exec builder")
					return self.getFile(stdout)
				},

				/**
				 * Retrieves the stderr file.
				 */
				getStderrFile: func() {
					ll.assert(stderrFile,
						"stderr file was not saved, add saveStderrFile() call to the exec builder")
					return self.getFile(stderr)
				},

				/**
				 * Retrieves the content of the stdout file.
				 */
				getStdoutContent: func() {
					ll.assert(stdoutContent,
						"stdout content was not saved, add saveStdoutContent() call to the exec builder")
					return self.getFileContent(stdout)
				},

				/**
				 * Retrieves the content of the stderr file.
				 */
				getStderrContent: func() {
					ll.assert(stderrContent,
						"stderr content was not saved, add saveStderrContent() call to the exec builder")
					return self.getFileContent(stderr)
				}
			}))

			return self
		}
	})

	return self
}

export ll.toStrict({
	builder: builder
})
