/**
 * Library providing xsv <> p-frames conversion utils.
 */

validation := import(":validation")
constants := import(":pframes.constants")
objects := import(":objects")
render := import(":render")
exec := import(":exec")
json := import("json")
util := import(":pframes.util")
ll := import(":ll")
assets := import(":assets")

transposeXsvSw := assets.importSoftware("@platforma-open/milaboratories.software-small-binaries:table-converter")
pfconvSw := assets.importSoftware("@milaboratories/software-pframes-conv:main")

/**
 * Calculate axes spec from pfconv spec
 */
getAxesSpec := func(spec) {
	axes := []
	for ax in spec.axes {
		axes = append(axes, ax.spec)
	}
	return axes
}

renderOps := func(defaults, ...ops) {
	o := { }

	for k, v in defaults {
		o[k] = v
	}

	if len(ops) > 0 {
		for k, v in ops[0] {
			o[k] = v
		}
	}
	return o
}

getColumnSpec := func(axesSpec, col) {
	spec := {
		kind: constants.KIND_P_COLUMN,
		axesSpec: axesSpec
	}

	for k, v in col.spec {
		spec[k] = v
	}

	return objects.deleteUndefined(spec)
}

/**
 * Imports xsv data into p-frame. The resulting map resource contains all columns specified in the params (column identifiers as
 * provided by the spec used as map keys). Resulting p-columns will be always single-partitioned at most.
 *
 * @param xsvFile: reference - a reference to a file
 * @param xsvType: string - either csv or tsv
 * @param spec: object - xsv conversion specification
 * @param ops: object - additional options
 *               {
 *                 dataOnly: boolean  - set to true to completely skip creation of specs
 *                 transpose: object - if specified, then the input table will be transposed
 *                            { 
 *                              separator: string - (optional) use separator instead of inferring from the file extension
 *                              pAxisIdx: index - (optional) the index of the primary axis (column in the input xsv); default 0
 *                              pAxisName: string - (optional) the name of the primary axis (column name in the input xsv); default undefined
 *                              pAxisSearch: regex - (optional) the regex pattern of the name of the primary axis (column name in the input xsv); default undefined
 *                              pAxisNameOverride: string - (optional) override primary axis header in the output; default undefined
 *                              sAxisSearch: string - (optional) regex to filter secondary axis columns in the input table;
 *                              sAxisName: string - (optional) the name of the secondary axis (column name in the output xsv); default "Metric"
 *                              valueName: string - (optional) the name of the value column; default "Value"
 *                              separatorOverride: string - (optional) specify separator in the output (optional)
 *                            }
 *               }
 * @return map: reference - a reference to a map resource storing imported data.
 */
importFile := func(xsvFile, xsvType, spec, ...ops) {
	ll.assert(xsvType == "csv" || xsvType == "tsv", "expected one of [tsv, csv] types")

	validation.assertJsonSchema(spec, util.PFCONV_CFG_SCHEMA)

	xsvFileName := "file." + xsvType

	ops := renderOps({ dataOnly: false }, ops...)

	if ops.transpose != undefined {

		tops := ops.transpose

		transposedFileName := "fileTransposed." + xsvType

		// convert csv to p-frame and read resulting data
		transposeXsv := exec.builder().
			software(transposeXsvSw).
			printErrStreamToStdout().
			inLightQueue()
			
		if (tops.separator != undefined) {
			transposeXsv = transposeXsv.arg("-input-separator").arg(tops.separator)
		}
		if (tops.pAxisIdx != undefined) {
			transposeXsv = transposeXsv.arg("-sample-column-i").arg("" + tops.pAxisIdx) // always convert to string
		}
		if (tops.pAxisName != undefined) {
			transposeXsv = transposeXsv.arg("-sample-column-name").arg(tops.pAxisName)
		}
		if (tops.pAxisSearch != undefined) {
			transposeXsv = transposeXsv.arg("-sample-column-search").arg(tops.pAxisSearch)
		}
		if (tops.pAxisNameOverride != undefined) {
			transposeXsv = transposeXsv.arg("-sample-label").arg(tops.pAxisNameOverride)
		}
		if (tops.sAxisSearch != undefined) {
			transposeXsv = transposeXsv.arg("-metric-columns-search").arg(tops.sAxisSearch)
		}
		if (tops.sAxisName != undefined) {
			transposeXsv = transposeXsv.arg("-metric-label").arg(tops.sAxisName)	
		}
		if (tops.valueName != undefined) {
			transposeXsv = transposeXsv.arg("-value-label").arg(tops.valueName)	
		}
		if (tops.separatorOverride != undefined) {
			transposeXsv = transposeXsv.arg("-separator").arg(tops.separatorOverride)	
		}
		
		transposeXsv = transposeXsv.
			arg(xsvFileName).
			arg(transposedFileName).
			addFile(xsvFileName, xsvFile).
			saveFile(transposedFileName).
			run()

		xsvFile = transposeXsv.getFile(transposedFileName)
	}

	// import p-frame from directory tpl
	importDirTpl := assets.importTemplate(":pframes.import-dir")

	// spec without any metadata (for caching purposes)
	pureSpec := util.purifySpec(spec)

	// convert csv to p-frame and read resulting data
	pfconv := exec.builder().
		software(pfconvSw).
		printErrStreamToStdout().
		inLightQueue().
		arg("importCsv").
		arg(xsvFileName).
		arg("-p").arg("spec.json").
		arg("-o").arg("out").
		addFile(xsvFileName, xsvFile).
		writeFile("spec.json", json.encode(pureSpec)).
		processWorkdir("pf", importDirTpl, pureSpec).
		run()

	// p-columns data
	pf := pfconv.getProcessorResult("pf")
	if ops.dataOnly {
		return pf
	}

	axesSpec := getAxesSpec(spec)

	result := {}
	for col in spec.columns {
		id := util.xsvColumnId(col)

		result[id + ".data"] = pf.getFutureInputField(id)
		result[id + ".spec"] = getColumnSpec(axesSpec, col)
	}

	return result
}

/**
 * Imports a map with xsv files into a p-frame. The resulting map resource contains all columns specified in the params (column identifiers as
 * provided by the spec used as map keys). Resulting p-columns may be double-partitioned.
 *
 * @param xsvFile: reference - a reference to a file
 * @param xsvType: string - either csv or tsv
 * @param spec: object - xsv conversion specification
 * @param ops: object - additional options
 *               {
 *                 dataOnly: boolean  - set to true to completely skip creation of specs
 *                 splitDataAndSpec: boolean  - if true, resulting map will have nested structures,
 *                                              with "spec" + "data" fields in nested maps
 *                 additionalAxesSpec: AxisSpec[]  - array of additional axes spec to prepend to
 *                                                   each column spec
 *                 transpose: object - if specified, then the input table will be transposed
 *                            { 
 *                              separator: string - (optional) use separator instead of inferring from the file extension
 *                              pAxisIdx: index - (optional) the index of the primary axis (column in the input xsv); default 0
 *                              pAxisName: string - (optional) the name of the primary axis (column name in the input xsv); default undefined
 *                              pAxisSearch: regex - (optional) the regex pattern of the name of the primary axis (column name in the input xsv); default undefined
 *                              pAxisNameOverride: string - (optional) override primary axis header in the output; default undefined
 *                              sAxisSearch: string - (optional) regex to filter secondary axis columns in the input table;
 *                              sAxisName: string - (optional) the name of the secondary axis (column name in the output xsv); default "Metric"
 *                              valueName: string - (optional) the name of the value column; default "Value"
 *                              separatorOverride: string - (optional) specify separator in the output (optional)
 *                            }
 *               }
 * @return map: reference - a reference to a map resource storing imported data.
 */
importFileMap := func(xsvMap, xsvType, spec, ...ops) {

	importXsvMapTpl := assets.importTemplate(":pframes.import-xsv-map")

	ops := renderOps({
	    dataOnly: false,
		splitDataAndSpec: false,
		additionalAxesSpec: [],
		transpose: undefined
	  }, ops...)

	// spec without any metadata (for caching purposes)
	decomposedSpec := util.decomposePfconvCfg(spec, { additionalAxesSpec: ops.additionalAxesSpec })
	pureSpec := decomposedSpec.purifiedCfg
	columnsSpec := decomposedSpec.columnsSpec

	r := render.createEphemeral(importXsvMapTpl, {
		xsvMap: xsvMap,
		xsvType: xsvType,
		spec: pureSpec,
		ops: { dataOnly: true, transpose: ops.transpose }
	})

	if ops.dataOnly && ops.splitDataAndSpec {
		ll.panic("Can't use both options dataOnly and splitDataAndSpec at the same time.")
	}

	result := {}
	axesSpec := getAxesSpec(spec)
	for c in spec.columns {
		id := util.xsvColumnId(c)
		if ops.dataOnly {
			result[id] = r.output(id)
		} else if ops.splitDataAndSpec {
			result[id] = {
				data: r.output(id),
				spec: columnsSpec[id]
			}
		} else {
			result[id + ".data"] = r.output(id)
			result[id + ".spec"] = columnsSpec[id]
		}
	}
	
	return result
}

export ll.toStrict({
	importFile: importFile,
	importFileMap: importFileMap
})
