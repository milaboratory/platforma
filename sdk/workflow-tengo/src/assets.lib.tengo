/** API to retrieve assets packed with template code (like other templates). */

plapi := import("plapi")
json := import("json")
ll := import(":ll")
smart := import(":smart")

/**
 * Returns internal template identifier for loaded templates
 *
 * @param tplName: string - full name of a template, including namespace (package name).
 * Local templates (within the same package) can omit namespace part and start the name with colon:
 *  - "@company/package:someTemplateName"
 *  - ":myLocalTemplate"
 *
 * @return template: smart.resource - resource of tengo template with given name.
 */
importTemplate := func(tplName) {
	return smart.resource(plapi.getTemplateId(tplName))
}

/**
 * Returns software description suitable for 'exec' operations.
 *
 * @param swName: string - full name of the software entrypoint, including namespace (NPM package name).
 *                         Local software entrypoints (within the same package) can omit namespace part
 *                         and start the name with colon:
 *                           - "@company/package:some-oftware"
 *                           - ":my-local-software"
 *
 * @return softwareInfo: softwareInfo - structure with software info.
 */
importSoftware := func(swName) {
	sw := plapi.getSoftwareInfo(swName)
	return ll.toStrict({
		_type: "softwareDescriptor",
		_id: sw.id,

		resource: smart.resource(sw.id),
		name: sw.name,
		version: sw.version,
		blobRef: sw.blobRef,
		descriptor: ll.toStrict(json.decode(sw.descriptor)) // raw sw.json data with software entrypoint descriptor
	})
}

/**
 * Checks if given item is a software info
 *
 * @param toCheck: any - item to check
 *
 * @return isSoftware: boolean - true for software info, loaded with importSoftware()
 */
isSoftwareInfo := func(toCheck) {
	if !ll.isMap(toCheck) {
		return false
	}

	return (ll.fromStrict(toCheck)._type) == "softwareDescriptor"
}

/**
 * Returns asset description suitable for 'exec' operations.
 * Asset is the way to put static files into working directory before execution of the command.
 *
 * @param assetName: string - full name of asset entrypoint, including namespace (NPM package name).
 *                            Local software entrypoints (within the same package) can omit namespace
 *                            part and start the name with colon:
 *                              - "@company/package:some-software"
 *                              - ":my-local-software"
 */
importAsset := func(assetName) {
	asset := plapi.getSoftwareInfo(assetName)
	return ll.toStrict({
		_type: "assetDescriptor",
		_id: asset.id,

		resource: smart.resource(asset.id),
		name: asset.name,
		version: asset.version,
		blobRef: asset.blobRef,
		descriptor: ll.toStrict(json.decode(asset.descriptor)) // raw sw.json data with asset entrypoint descriptor
	})
}

/**
 * Checks if given item is an asset info
 *
 * @param toCheck: any - item to check
 *
 * @return isAsset: boolean - true for asset info, loaded with importAsset()
 */
isAssetInfo := func(toCheck) {
	if !ll.isMap(toCheck) {
		return false
	}

	return (ll.fromStrict(toCheck)._type) == "assetDescriptor"
}

export ll.toStrict({
	importTemplate: importTemplate,
	importSoftware: importSoftware,
	isSoftwareInfo: isSoftwareInfo,
	importAsset:    importAsset,
	isAssetInfo:    isAssetInfo
})
