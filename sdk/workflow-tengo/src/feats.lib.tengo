/**
 * Catalogue of features available in current execution context.
 */
plapi := import("plapi")
ll := import(":ll")

_api := plapi.apiVersion
if is_undefined(_api) {
	_api = 0
}

_isEnabled := func(featName) {
	_checker := plapi.isFeatureEnabled
	if is_undefined(_checker) {
		return false
	}

	return _checker(featName)
}

_executors := undefined
_hasRunner := func(e_id) {
	if is_undefined(_executors) {
		_executors = {}
		for e in plapi.getExecutors() {
			_executors[e] = true
		}
	}

	return !is_undefined(_executors[e_id])
}

// Check if getRunnerFor method is available in current backend
_hasGetRunnerFor := ll.methodExists(plapi, "getRunnerFor")

// Get runner type for a specific queue from backend.
// Returns the runner type (e.g., "executor", "batch") or undefined if not available.
_getRunnerFor := func(queueName) {
	if !_hasGetRunnerFor {
		return undefined
	}
	return plapi.getRunnerFor(queueName)
}

export ll.toStrict({
	hasExec:                    _hasRunner("executor"),     // local binary execution is available
	hasBatch:                   _hasRunner("batch"),        // batch system execution is available

	// get runner type for a specific queue (returns undefined if backend doesn't support it)
	getRunnerFor:               _getRunnerFor,

	pureFutureFields:           _isEnabled("pureFutureFields")   || _api >= 1, // 'pure' future fields magic that provides deduplication
	commandExpressions:         _isEnabled("commandExpressions") || _api >= 2, // rich expressions in command arguments ( {{ system.ram_bytes / 20 }} )

	// false for backend versions where service fields were not exposed to the template
	serviceFields: _isEnabled("serviceFields"),

	// - template can subscirbe to ResourceDuplicate, FieldGotError, OutputSet, AllOutputsSet and GenericOTWSet.
	// - hashing funcs: sha256Encode, gzipEncode, gzipDecode, base32Encode, base32HexEncode, base32Decode, base32HexDecode
	// - execution context: not persistent context for global variables.
	// - missing tx methods.
	fullFeaturedApi:        _isEnabled("fullFeaturedAPI"),

	// true when backend supports blob size retrieval via ll.getBlobSize()
	hasBlobSize:            ll.methodExists(plapi, "getBlobSize"),

	// true when backend supports docker packages
	isDockerAvailable:        _isEnabled("dockerSupport")
})
